// Simple paired associatives task
// Model is shown a word and has to press the right finger

define task paired-associate{
    initial-skills: (paired-associate)
    opc: t
    explore-exploit: 0.5

    ans: 0.2 //Activation noise
 	ol: t //optimized version (Base-level activation)
  	default-activation: 1.0 //Base-level activation
  	bll: 0.5 //base-level decay

  	// Spread activation from goals
  	default-operator-assoc: 4.0
  	goal-operator-learning: t
  	operator-bll: nil
  	beta: 0.1
  	reward: 10.0

  	//Inter operators learning
  	default-inter-operator-assoc: 0.0
  	default-operator-self-assoc: 0.0
  	inter-operator-learning: nil

  	//Spread activation from chunks
  	goal-chunk-spreads: nil
  	ga: 1.0
  	input-activation: 2.0
  	retrieval-activation: 4.0
  	imaginal-activation: 0.0
  	mas: 3.0


    //Retrieval duration
  	lf: 0.2 //latency factor F
  	rt: -2.0 //retrieval threshold Tau
}

define goal primitives {

operator V1toRT1 {
  V1 <> nil
==>
    V1 -> RT1
}

operator V1toRT2 {
  V1 <> nil
==>
    V1 -> RT2
}

operator V1toRT3 {
  V1 <> nil
==>
    V1 -> RT3
}


operator V2toRT1 {
  V2 <> nil
==>
    V2 -> RT1
}

operator V2toRT2 {
  V2 <> nil
==>
    V2 -> RT2
}

operator V2toRT3 {
  V2 <> nil
==>
    V2 -> RT3
}

operator RT1toAC1 {
  RT1 <> nil
==>
  action -> AC1
  RT1 -> AC2
}

operator RT2toAC1 {
  RT2 <> nil
==>
  action -> AC1
  RT2 -> AC2
}

operator RT3toAC1 {
  RT3 <> nil
==>
  action -> AC1
  RT3 -> AC2
}


operator RT2toRT1 {
  RT2 <> nil
==>
  RT2 -> RT1
}

operator RT3toRT2 {
  RT3 <> nil
==>
  RT3 -> RT2
}
  
operator V1toWM1 {
  V1 <> nil
  WM1 = nil
==>
   V1 -> WM1
}

operator V2toWM1 {
  V2 <> nil
  WM1 = nil
==>
   V2 -> WM1
}

operator RT2toWM1 {
  RT2 <> nil
  WM1 = nil
==>
  RT2 -> WM1
}

operator RT3toWM1 {
  RT3 <> nil
  WM1 = nil
==>
  RT3 -> WM1
}

operator WM1toRT1 {
  RT1 = nil
  WM1 <> nil
==>
    WM1 -> RT1
//    nil -> WM1
}

operator WM1toRT2 {
  RT2 = nil
  WM1 <> nil
==>
    WM1 -> RT2
//    nil -> WM1
}

operator WM1toRT3 {
  RT3 = nil
  WM1 <> nil
==>
    WM1 -> RT3
//    nil -> WM1
}

operator WM1toAC1 {
  WM1 <> nil
==>
  action -> AC1
    WM1 -> AC2
//    nil -> WM1
}


operator C1toRT1 {
  G1 <> nil
==>
  *fact-type -> RT1
}

operator C1altToRT1 {
  G1 <> nil
==>
  *fact-type-two -> RT1
}

operator RT1equalC1 {
  RT1 = *fact-type
==>
}

operator RT1equalC1alt {
  RT1 = *fact-type-two
==>
}

// Operators that implement multiple states

operator stage1 {
  G2 = state-one
  ==>
  state-two -> G2
}
  
operator stage2 {
  G2 = state-two
  ==>
  state-three -> G2
}

// Operators that model a transition to a new goal

// We keep the old goal in G1 to ensure credit assignment is maintained

operator next-goal {
  G1 <> nil
  G2 = nil
==>
  *next-goal-type -> G2
}


}


define action say {
  latency: 0.3
  noise: 0.1
  distribution: uniform
  output: Saying
}

define facts {
  (vanilla k x)
  (thumb j x)
  (ice i x)
  (ring r x)
  (paper m x)
  (pinkie s x)
  (associate o x)
  (taste e x)
  (temperature h x)
  (weight g x)
  (texture w x)
  (color v x)
}


define facts {
  (f1 associate vanilla thumb)
  (f2 associate ice ring)
  (f3 associate paper pinkie)
  (fa1 taste vanilla sweet)
  (fa2 temperature ice cold)
  (fa3 weight paper small)
  (fb1 color vanilla yellow)
  (fb2 texture ice slippery)
  (fb3 color paper white)
}

define script {
	trial-start()
	set-goal("paired-associate",["fact-type","associate"])
	stimuli = ["vanilla", "ice", "paper"]
	responses = ["thumb", "ring", "pinkie"]
    pair = random(3)
	
    print("Testing",stimuli[pair],"and",responses[pair])
    screen(stimuli[pair])
    run-step()
    screen("nil")

    run-until-relative-time-or-action(5.0, "action")
    ac = last-action()
    print(ac)

    if (ac[0] != "action") {
		issue-reward(-0.5)
		print("Out of time or Illegal action.")
		plot-point(0)
		data-line("0")
	}  else {	
		if (ac[1] == responses[pair]) {
			issue-reward()
			print("Correct!")
			plot-point(1)

		} else {
			issue-reward(-1)
			print("Wrong")
			plot-point(0)
		}   
	}
    trial-end()
    sleep(5.0)
}